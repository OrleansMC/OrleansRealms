plugins {
    id "io.github.goooler.shadow" version "8.1.7"
    id 'java'
}

group = 'com.orleansmc'
version = '0.0.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name "eldonexus"
        url "https://eldonexus.de/repository/maven-releases/"
        content {
            includeModule("com.github.yannicklamprecht", "worldborderapi")
        }
    }
    maven { url "https://repo.codemc.io/repository/maven-public/" }
    maven { url 'https://repo.william278.net/releases' }
    maven { url 'https://maven.enginehub.org/repo/' }
    maven {
        name = "jitpack"
        url = "https://jitpack.io/"
    }
    maven {
        name = 'citizens-repo'
        url = 'https://maven.citizensnpcs.co/repo'
    }
}

shadowJar {
    relocate 'com.github.stefvanschie.inventoryframework', 'com.orleansmc.realms.inventoryframework'
    minimize()
}
jar.enabled = false
assemble.dependsOn(shadowJar)

dependencies {
    compileOnly(fileTree("${project.rootDir}/lib") { include("*.jar") })
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.16'
    compileOnly 'me.pikamug.quests:quests-core:5.1.1'
    compileOnly 'com.github.decentsoftware-eu:decentholograms:2.8.11'
    compileOnly 'net.skinsrestorer:skinsrestorer-api:15.4.0'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly "io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT"
    compileOnly 'com.github.LoneDev6:itemsadder-api:3.6.3-beta-14'
    compileOnly 'me.lucko:helper-redis:1.2.0'
    compileOnly 'me.lucko:helper-mongo:1.2.0'
    compileOnly 'me.lucko:helper:5.6.14'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly "com.github.yannicklamprecht:worldborderapi:1.202.0:dev"// https://mvnrepository.com/artifact/io.lettuce/lettuce-core
    compileOnly 'io.lettuce:lettuce-core:6.4.0.RELEASE'
    compileOnly "net.william278.huskhomes:huskhomes-bukkit:4.7"
    compileOnly 'org.popcraft:chunky-common:1.4.17'
    compileOnly 'com.github.Emibergo02:RedisEconomy:4.3.9'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly 'net.luckperms:api:5.4'
    compileOnly('net.citizensnpcs:citizens-main:2.0.35-SNAPSHOT') {
        exclude group: '*', module: '*'
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
